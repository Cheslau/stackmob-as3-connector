<?xml version="1.0" encoding="utf-8"?>
<s:Application xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
			   applicationComplete="application1_applicationCompleteHandler(event)" applicationDPI="160">
	<fx:Script>
		<![CDATA[
			import com.cycleit.stackmobconn.event.LoginEvent;
			import com.cycleit.stackmobconn.net.StackMobService;

			import mx.events.FlexEvent;

			// Your public key
			public const XStackMobAPIKey:String = "";

			// Username. ie Bruce Wayne
			public const username:String = "";

			// Password. ie iambatman
			public const password:String = "";

			private var smService:StackMobService;

			protected function application1_applicationCompleteHandler(event:FlexEvent):void {
				smService = new StackMobService(XStackMobAPIKey, StackMobService.DEVELOPMENT_VERSION);
				smService.addEventListener(LoginEvent.LOGIN, loginHandler);
				smService.addEventListener(LoginEvent.LOGIN_ERROR, function(event:Event):void {
					trace(event);
					trace(smService.data);
				});
				smService.login(username, password);

			}

			private function loginHandler(event:LoginEvent):void {
				trace(event);
				smService.addEventListener(Event.COMPLETE, completeHandler);
				smService.get("blogentry");
			}

			private function completeHandler(event:Event):void {
				smService.removeEventListener(Event.COMPLETE, completeHandler);
				smService.addEventListener(Event.COMPLETE, createHandler);
				smService.addEventListener(IOErrorEvent.IO_ERROR, errorHandler);
				smService.post("blogentry", {"message":"SM AS3 SDK 0.0.1 Test"});
			}

			private function createHandler(event:Event):void {
				var newEntry:Object = smService.data;
				smService.removeEventListener(Event.COMPLETE, createHandler);
				smService.addEventListener(Event.COMPLETE, getEntryHandler);
				smService.get("blogentry/" + newEntry.blogentry_id);
			}

			private function getEntryHandler(event:Event):void {
				var entry:Object = smService.data;
				smService.removeEventListener(Event.COMPLETE, getEntryHandler);
				smService.addEventListener(Event.COMPLETE, updateEntryHandler);
				smService.put("blogentry/" + entry.blogentry_id, {"message":"Updated Message for AS3 SDK!"});
			}

			private function updateEntryHandler(event:Event):void {
				var entry:Object = smService.data;
				smService.removeEventListener(Event.COMPLETE, updateEntryHandler);
				var params:URLVariables = new URLVariables();
				params.blogentry_id = entry.blogentry_id;
				smService.del("blogentry", params);
			}

			private function errorHandler(event:IOErrorEvent):void {
				trace(smService.data);
			}
		]]>
	</fx:Script>
</s:Application>
